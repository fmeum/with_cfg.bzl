load("@bazel_features//:features.bzl", "bazel_features")
load("@rules_shell//shell:sh_test.bzl", "sh_test")
load("//dynamic_deps/rules:runfiles_dir.bzl", "runfiles_dir")
load(":cc_define_binary.bzl", "cc_define_binary")

cc_define_binary(
    name = "bin",
    srcs = ["bin.cpp"],
    dynamic_deps = ["//dynamic_deps/lib:shared"],
    deps = ["//dynamic_deps/lib"],
)

runfiles_dir(
    name = "bin_runfiles",
    executable = ":bin",
)

sh_test(
    name = "bin_test_from_symlinked_runfiles",
    srcs = ["bin_test.sh"],
    args = ["$(rlocationpath :bin)"],
    data = [":bin"],
    deps = ["@bazel_tools//tools/bash/runfiles"],
)

sh_test(
    name = "bin_test_direct",
    srcs = ["bin_test.sh"],
    args = select({
        "@platforms//os:windows": ["$(rlocationpath :bin_runfiles)/bin.exe"],
        "//conditions:default": ["$(rlocationpath :bin_runfiles)/bin"],
    }),
    data = [":bin_runfiles"],
    target_compatible_with = select({
        # TODO: Get this test to pass on Windows.
        "@platforms//os:windows": ["@platforms//:incompatible"],
        # Requires https://github.com/bazelbuild/bazel/pull/23089 and the flip
        # of --incompatible_macos_set_install_name on macOS. Use a feature
        # introduced in Bazel 8 as a proxy for the fix.
        "@platforms//os:macos": [] if bazel_features.rules.rule_extension_apis_available else ["@platforms//:incompatible"],
        "//conditions:default": [],
    }),
    deps = ["@bazel_tools//tools/bash/runfiles"],
)

sh_test(
    name = "bin_test_from_regular_runfiles",
    srcs = ["bin_test.sh"],
    args = select({
        "@platforms//os:windows": ["$(rlocationpath :bin_runfiles)/bin.exe.runfiles/$(rlocationpath :bin)"],
        "//conditions:default": ["$(rlocationpath :bin_runfiles)/bin.runfiles/$(rlocationpath :bin)"],
    }),
    data = [
        ":bin",
        ":bin_runfiles",
    ],
    deps = ["@bazel_tools//tools/bash/runfiles"],
)
