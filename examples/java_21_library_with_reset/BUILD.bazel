load("@rules_java//java:java_binary.bzl", "java_binary")
load("@rules_java//java:java_library.bzl", "java_library")
load("@with_cfg.bzl", "original_settings")
load(":java_21_library.bzl", "java_21_library", "java_21_library_reset", "java_21_test")

java_binary(
    name = "Main",
    srcs = ["src/main/java/com/example/Main.java"],
    deps = [":primes"],
)

java_21_library(
    name = "primes",
    srcs = ["src/main/java/com/example/Primes.java"],
    # Only used to verify that with_cfg.bzl supports the toolchains attribute on
    # non-executable rules. Ignore for the purposes of this example.
    toolchains = [
        "@rules_java//toolchains:current_java_runtime",
    ],
    deps = [":integer_type"],
)

java_21_library(
    name = "integer_type",
    srcs = ["src/main/java/com/example/IntegerType.java"],
    deps = [":legacy_lib_reset"],
)

# The JDK 21 used for compilation with java_21_library doesn't support Java 7,
# so a reset to the top-level settings is required to depend on this "legacy"
# dependency.
java_21_library_reset(
    name = "legacy_lib_reset",
    exports = ":legacy_lib",
)

java_library(
    name = "legacy_lib",
    srcs = ["src/main/java/com/example/LegacyIllegalArgumentException.java"],
    javacopts = [
        "-Xlint:-options",
        "-source",
        "7",
        "-target",
        "7",
    ],
)

java_library(
    name = "testonly_legacy_lib",
    testonly = True,
    srcs = ["src/main/java/com/example/LegacyIllegalArgumentException.java"],
)

java_21_test(
    name = "tests",
    srcs = ["src/test/java/com/example/LegacyIllegalArgumentExceptionTest.java"],
    test_class = "com.example.LegacyIllegalArgumentExceptionTest",
    deps = [":testonly_legacy_lib"],
)

# This target exists purely to demonstrate (and verify) that implicit outputs of
# the underlying rule, in this case source jars, are available on the rule
# produced by with_cfg.
alias(
    name = "primes_sources",
    actual = "libprimes-src.jar",
)

original_settings(
    name = "java_21_library_original_settings",
)

original_settings(
    name = "java_21_test_original_settings",
)
