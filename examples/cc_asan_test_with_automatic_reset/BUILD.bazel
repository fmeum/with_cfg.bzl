load("@rules_cc//cc:cc_library.bzl", "cc_library")
load("@with_cfg.bzl", "original_settings")
load("//helpers:defs.bzl", "untransitioned_target")
load(":cc_asan_test.bzl", "cc_asan_test", "cc_asan_test_reset")

cc_asan_test(
    name = "asan_test",
    srcs = ["asan_test.cpp"],
    # These synthetic references to targets demonstrate that attributes that are
    # reset still support complex values such as selects.
    data = [
        ":untransitioned_target_1",
        ":untransitioned_target_2",
    ] + select({
        "@platforms//os:windows": [
            ":untransitioned_target_3",
            ":untransitioned_target_4",
        ],
        "//conditions:default": [
            ":untransitioned_target_5",
            ":untransitioned_target_6",
        ],
    }),
    env = {
        # Effectively invert the exit code so that the test passes if and only
        # if the expected ASAN error is detected.
        "ASAN_OPTIONS": "exitcode=0:abort_on_error=0",
    },
    # Verify that an attr.label (rather than an attr.label_list) is handled correctly, this is
    # otherwise a no-op.
    win_def_file = ":asan_test.def",
    deps = [
        ":lib",
    ],
)

cc_library(
    name = "lib",
    srcs = ["lib.cpp"],
    hdrs = ["lib.h"],
    deps = [":large_dep_reset"],
)

# This ensures that the "large" third-party dependency is built with the
# original top-level settings, that is, without AddressSanitizer.
cc_asan_test_reset(
    name = "large_dep_reset",
    exports = "//cc_asan_test_with_automatic_reset/third_party:large_dep",
)

original_settings(
    name = "cc_asan_test_original_settings",
)

# These synthetic targets verify that they are not affected by any
# transition.
[
    untransitioned_target(
        name = "untransitioned_target_" + str(i),
    )
    for i in range(1, 7)
]
