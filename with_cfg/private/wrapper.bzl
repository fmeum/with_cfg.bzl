load("@bazel_features//:features.bzl", "bazel_features")
load(":args.bzl", "rewrite_args")
load(":frontend.bzl", "unwrap_template_variable_info")
load(":rewrite.bzl", "make_label_rewriter", "rewrite_locations_in_attr")
load(":select.bzl", "map_attr")
load(":setting.bzl", "validate_and_get_attr_name")
load(":utils.bzl", "is_dict", "is_label", "is_list", "is_string")

visibility("private")

# buildifier: disable=unnamed-macro
def make_wrapper(
        *,
        rule_info,
        frontend,
        transitioning_alias,
        values,
        original_settings_label,
        attrs_to_reset):
    wrapper_impl = lambda *, name, **kwargs: _wrapper(
        name = name,
        kwargs = kwargs,
        rule_info = rule_info,
        frontend = frontend,
        transitioning_alias = transitioning_alias,
        values = values,
        original_settings_label = original_settings_label,
        attrs_to_reset = attrs_to_reset,
    )
    if bazel_features.globals.macro and rule_info.supports_inheritance:
        return bazel_features.globals.macro(
            implementation = wrapper_impl,
            inherit_attrs = rule_info.kind,
        )
    return wrapper_impl

# Attributes common to all rules.
# These attributes are applied to all targets generated by the wrapper.
# testonly, tags and visibility are handled specially.
# toolchains and args need special handling since they are evaluated by
# hardcoded location and Make variable expansion logic that needs heavy hacks
# to replicate the wrapped rule's expansion context.
# exec_properties are treated specially since they may refer to different exec
# groups, some of which aren't available on the frontend. The same applies to
# exec_group_compatible_with.
_COMMON_ATTRS = [
    # keep sorted
    "compatible_with",
    "deprecation",
    "exec_compatible_with",
    "features",
    "restricted_to",
    "target_compatible_with",
]

# Attributes common to all test rules.
# These attributes are applied to the original target and the frontend if the original target is a
# test.
# args is treated specially.
# env and env_inherit are covered by the forwarded RunEnvironmentInfo instead.
_TEST_ATTRS = [
    # keep sorted
    "flaky",
    "local",
    "shard_count",
    "size",
    "timeout",
]

def _wrapper(
        *,
        name,
        kwargs,
        rule_info,
        frontend,
        transitioning_alias,
        values,
        original_settings_label,
        attrs_to_reset):
    dirname, separator, basename = name.rpartition("/")
    original_name = "{dirname}{separator}{basename}_/{basename}".format(
        dirname = dirname,
        separator = separator,
        basename = basename,
    )
    alias_name = name + "_with_cfg"

    tags = kwargs.pop("tags", None)
    if not tags:
        tags_with_manual = ["manual"]
    elif "manual" not in tags:
        tags_with_manual = tags + ["manual"]
    else:
        tags_with_manual = tags

    # Exec properties can refer to specific exec groups and referencing an undefined exec group is
    # an error. We thus have to extract those that may apply to the frontend (only ever "test").
    # Since the intermediate targets don't have any interesting actions, they don't need any exec
    # properties. The same applies to exec_group_compatible_with, which is non-configurable.
    frontend_exec_properties = map_attr(
        lambda d: {
            k: v
            for k, v in d.items()
            if "." not in k or k.startswith("test.")
        },
        kwargs.get("exec_properties", {}),
    )
    frontend_test_exec_group_compatible_with = kwargs.get("exec_group_compatible_with", {}).get("test")

    visibility = kwargs.pop("visibility", None)
    common_attrs = {
        attr: kwargs.pop(attr)
        for attr in _COMMON_ATTRS
        if attr in kwargs
    }

    # Due to --trim_test_configuration, all targets that depend on tests (such as our
    # transitioning_alias) must be marked as testonly to avoid action conflicts.
    if rule_info.test:
        common_attrs["testonly"] = True
    elif "testonly" in kwargs:
        common_attrs["testonly"] = kwargs.pop("testonly")

    if rule_info.test:
        extra_attrs = {
            attr: kwargs.pop(attr)
            for attr in _TEST_ATTRS
            if attr in kwargs
        }
    else:
        extra_attrs = {}

    # Native rules use magic "env" and "env_inherit" attributes to set environment variables. We
    # have to implement these attributes manually as they aren't forwarded via RunEnvironmentInfo.
    if rule_info.native and (rule_info.executable or rule_info.test):
        if "env" in kwargs:
            extra_attrs["env"] = kwargs.pop("env")
        if "env_inherit" in kwargs:
            extra_attrs["env_inherit"] = kwargs.pop("env_inherit")

    frontend_attrs = {}
    if frontend_exec_properties:
        frontend_attrs["exec_properties"] = frontend_exec_properties
    if frontend_test_exec_group_compatible_with:
        frontend_attrs["exec_group_compatible_with"] = {
            "test": frontend_test_exec_group_compatible_with,
        }
    if "args" in kwargs and (rule_info.executable or rule_info.test):
        # Leave the args attribute in place on the original rule so that they can be read by the
        # args_aspect attached to the exports attribute of the transitioning_alias.
        frontend_attrs["args"], frontend_attrs["data"] = rewrite_args(
            alias_name,
            kwargs["args"],
            lambda *, name, srcs, output_group: native.filegroup(
                name = name,
                srcs = srcs,
                output_group = output_group,
                testonly = common_attrs.get("testonly", False),
                tags = ["manual"],
                visibility = ["//visibility:private"],
            ),
        )
    if "toolchains" in kwargs and (rule_info.executable or rule_info.test):
        unwrap_name = name + "__unwrap_template_variable_info"
        unwrap_template_variable_info(
            name = unwrap_name,
            exports = ":" + alias_name,
            testonly = common_attrs.get("testonly", False),
            tags = ["manual"],
            visibility = ["//visibility:private"],
        )
        frontend_attrs["toolchains"] = [":" + unwrap_name]

    processed_kwargs = _process_attrs_for_reset(
        attrs = kwargs,
        attrs_to_reset = attrs_to_reset,
        reset_target = lambda *, name, exports: transitioning_alias(
            name = name,
            exports = exports,
            testonly = common_attrs.get("testonly", False),
            tags = ["manual"],
            visibility = ["//visibility:private"],
        ),
        name_prefix = name,
    )
    rule_info.kind(
        name = original_name,
        tags = tags_with_manual,
        visibility = ["//visibility:private"],
        **(processed_kwargs | common_attrs | extra_attrs)
    )

    alias_attrs = {
        validate_and_get_attr_name(setting): value
        for setting, value in values.items()
    }
    if original_settings_label:
        alias_attrs["internal_only_reset"] = False

    transitioning_alias(
        name = alias_name,
        exports = ":" + original_name,
        tags = tags_with_manual,
        visibility = ["//visibility:private"],
        **(alias_attrs | common_attrs)
    )

    frontend(
        name = name,
        exports = ":" + alias_name,
        tags = tags,
        visibility = visibility,
        **(common_attrs | extra_attrs | frontend_attrs)
    )

    for implicit_target in rule_info.implicit_targets:
        dirprefix = dirname + separator
        sub_name = implicit_target.format(
            dirprefix = dirprefix,
            basename = basename,
            name = dirprefix + basename,
        )
        original_dirprefix = dirprefix + basename + "_/"
        original_sub_name = implicit_target.format(
            dirprefix = original_dirprefix,
            basename = basename,
            name = original_dirprefix + basename,
        )
        transitioning_alias(
            name = sub_name,
            exports = ":" + original_sub_name,
            tags = tags_with_manual,
            visibility = visibility,
            **(alias_attrs | common_attrs)
        )

def _process_attrs_for_reset(*, attrs, attrs_to_reset, reset_target, name_prefix):
    if not attrs_to_reset:
        return attrs

    # In a first pass over only the attributes to reset, replace all labels representing
    # dependencies with labels of a `reset_target` target that forwards the dep's providers while
    # applying the reset transition. Along the way collect a map of original to new labels.
    label_map = {}
    first_pass_attrs = dict(attrs)
    for attr in attrs_to_reset:
        if not attr in attrs:
            continue
        mutable_num_calls = [0]
        attr_func = lambda dep: _replace_dep_attr(
            dep = dep,
            label_map = label_map,
            reset_target = reset_target,
            name_prefix = name_prefix + "__" + attr,
            mutable_num_calls = mutable_num_calls,
        )
        first_pass_attrs[attr] = map_attr(attr_func, attrs[attr])

    # In a second pass, now over all attributes, rewrite all $(location ...) expressions. These can
    # even appear in attributes that are reset (as values in attr.label_keyed_string_dict).
    second_pass_attrs = {}
    label_rewriter = make_label_rewriter(label_map)
    for attr, value in first_pass_attrs.items():
        second_pass_attrs[attr] = rewrite_locations_in_attr(value, label_rewriter)

    return second_pass_attrs

def _replace_dep_attr(*, dep, label_map, reset_target, name_prefix, mutable_num_calls):
    if dep == None:
        return dep
    if is_list(dep):
        # attr.label_list
        return [
            _replace_single_dep(
                label_string = label_string,
                label_map = label_map,
                reset_target = reset_target,
                name_prefix = name_prefix,
                mutable_num_calls = mutable_num_calls,
            )
            for label_string in dep
        ]
    if is_dict(dep):
        # attr.label_keyed_string_dict (only the keys represent deps)
        return {
            _replace_single_dep(
                label_string = label_string,
                label_map = label_map,
                reset_target = reset_target,
                name_prefix = name_prefix,
                mutable_num_calls = mutable_num_calls,
            ): v
            for label_string, v in dep.items()
        }

    # attr.label
    return _replace_single_dep(
        label_string = dep,
        label_map = label_map,
        reset_target = reset_target,
        name_prefix = name_prefix,
        mutable_num_calls = mutable_num_calls,
    )

def _replace_single_dep(
        *,
        label_string,
        label_map,
        reset_target,
        name_prefix,
        mutable_num_calls):
    use_label = is_label(label_string)
    if not use_label and not is_string(label_string):
        fail("Expected dependency, got '{}' of type {}".format(label_string, type(label_string)))
    label = native.package_relative_label(label_string)
    if label in label_map:
        target_label_string = label_map[label]
    else:
        # Multiple targets can be referenced in a single logical dep if that dep is a select.
        target_name = name_prefix + "_" + str(mutable_num_calls[0])
        reset_target(
            name = target_name,
            exports = label,
        )
        target_label_string = ":" + target_name
        label_map[label] = target_label_string
        mutable_num_calls[0] += 1

    return native.package_relative_label(target_label_string) if use_label else target_label_string
